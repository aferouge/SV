% All possible states for the bridge components
sort
  st_sign    = struct on | off;             %sign status
  st_deck    = struct up | down;            %deck status
  st_barrier = struct lift | lower;         %barrier status
  st_lock    = struct engaged | disengaged; %lock status
  st_bridge  = struct open | close;

  sign_id = struct P1 | P2 | P3 | P4;

% Flip actions to make a bridge component alter his status
% SIGNS
map
  flip_sign: st_sign -> st_sign;
eqn
  flip_sign(on) = off;
  flip_sign(off) = on;

% DECK
map
  flip_deck: st_deck -> st_deck;
eqn
  flip_deck(up) = down;
  flip_deck(down) = up;

% FRONT BARRIERS
map
  flip_barfront: st_barrier -> st_barrier;
eqn
  flip_barfront(lift) = lower;
  flip_barfront(lower) = lift;

% BACK BARRIERS
map
  flip_barback: st_barrier -> st_barrier;
eqn
  flip_barback(lift) = lower;
  flip_barback(lower) = lift;

% LOCKS
map
  flip_lock: st_lock -> st_lock;
%  flip_lockL2: st_lock -> st_lock;

var
  st: st_lock;

eqn
  flip_lock(engaged) = disengaged;
  flip_lock(disengaged) = engaged;
%  flip_lockL2(engaged) = engaged;
%  flip_lockL2(disengaged) = disengaged;


act
  sendPre, receivePre, commPre: st_sign;
  rSwitchOffPre, sSwitchOffPre, cSwitchOffPre;
  rSwitchOnPre, sSwitchOnPre, cSwitchOnPre;
  setPreSign: st_sign;

proc
  PreSign(status: st_sign)
    = (status == on) -> rSwitchOffPre . PreSign(status = off)
    + (status == off) -> rSwitchOnPre . PreSign(status = on)
    + sendPre(status) . PreSign();

act
  sendStop, receiveStop, commStop: st_sign;
  rSwitchOffStop, sSwitchOffStop, cSwitchOffStop;
  rSwitchOnStop, sSwitchOnStop, cSwitchOnStop;
  setStopSign: st_sign;

proc
  StopSign(status: st_sign)
    = (status == on) -> rSwitchOffStop . StopSign(status = off)
    + (status == off) -> rSwitchOnStop . StopSign(status = on)
    + sendStop(status) . StopSign();

act
  sendFrontBarrier, receiveFrontBarrier, commFrontBarrier: st_barrier;
  rLiftFrontBarrier, sLiftFrontBarrier, cLiftFrontBarrier;
  rLowerFrontBarrier, sLowerFrontBarrier, cLowerFrontBarrier;
  setFrontBarrier: st_barrier;

proc
  FrontBarrier(status: st_barrier)
    = (status == lower) -> rLiftFrontBarrier . FrontBarrier(status = lift)
    + (status == lift) -> rLowerFrontBarrier . FrontBarrier(status = lower)
    + sendFrontBarrier(status) . FrontBarrier();

act
  sendBackBarrier, receiveBackBarrier, commBackBarrier: st_barrier;
  rLiftBackBarrier, sLiftBackBarrier, cLiftBackBarrier;
  rLowerBackBarrier, sLowerBackBarrier, cLowerBackBarrier;
  setBackBarrier: st_barrier;

proc
  BackBarrier(status: st_barrier)
    = (status == lower) -> rLiftBackBarrier . BackBarrier(status = lift)
    + (status == lift) -> rLowerBackBarrier . BackBarrier(status = lower)
    + sendBackBarrier(status) . BackBarrier();

act
  sendLock, receiveLock, commLock: st_lock;
  rEngage, sEngage, cEngage;
  rDisengage, sDisengage, cDisengage;
  setLock: st_lock;

proc
  Lock(status: st_lock)
    = (status == engaged) -> rDisengage . Lock(status = disengaged)
    + (status == disengaged) -> rEngage . Lock(status = engaged)
    + sendLock(status) . Lock();

act
  sendDeck, receiveDeck, commDeck: st_deck;
  sLiftDeck, rLiftDeck, cLiftDeck;
  sLowerDeck, rLowerDeck, cLowerDeck;
  setDeck: st_deck;

proc
  Deck(status: st_deck)
    = (status == up) -> rLowerDeck . Deck(status = down)
    + (status == down) -> rLiftDeck . Deck(status = up)
    + sendDeck(status) . Deck();

proc
  SafetyLayer
    = setPreSign(on) . sSwitchOnPre . SafetyLayer()
    + setPreSign(off) . receiveStop(off) . sSwitchOffPre . SafetyLayer()

    + setStopSign(on) . receivePre(on) . sSwitchOnStop . SafetyLayer()
    + setStopSign(off) . receiveFrontBarrier(lift) . sSwitchOffStop . SafetyLayer()

    + setFrontBarrier(lower) . receiveStop(on) . sLowerFrontBarrier . SafetyLayer()
    + setFrontBarrier(lift) . receiveBackBarrier(lift) . sLiftFrontBarrier . SafetyLayer()
    + setBackBarrier(lower) . receiveFrontBarrier(lower) . sLowerBackBarrier . SafetyLayer()
    + setBackBarrier(lift) . receiveLock(engaged) . sLiftBackBarrier . SafetyLayer()

    + setLock(engaged) . receiveDeck(down) . sEngage . SafetyLayer()
    + setLock(disengaged) . receiveBackBarrier(lower) . sDisengage . SafetyLayer()

    + setDeck(up). receiveLock(disengaged) . sLiftDeck . SafetyLayer()
    + setDeck(down). receiveLock(disengaged) . sLowerDeck . SafetyLayer();

init
  allow(
    {
      setPreSign,
      commPre,
      cSwitchOffPre,
      cSwitchOnPre,

      setStopSign,
      commStop,
      cSwitchOffStop,
      cSwitchOnStop,

      setFrontBarrier,
      commFrontBarrier,
      cLowerFrontBarrier,
      cLiftFrontBarrier,

      setBackBarrier,
      commBackBarrier,
      cLowerBackBarrier,
      cLiftBackBarrier,

      setLock,
      commLock,
      cEngage,
      cDisengage,

      setDeck,
      commDeck,
      cLowerDeck,
      cLiftDeck
    },
    comm({
        receivePre|sendPre -> commPre,
        rSwitchOffPre|sSwitchOffPre -> cSwitchOffPre,
        rSwitchOnPre|sSwitchOnPre -> cSwitchOnPre,

        receiveStop|sendStop -> commStop,
        rSwitchOffStop|sSwitchOffStop -> cSwitchOffStop,
        rSwitchOnStop|sSwitchOnStop -> cSwitchOnStop,

        receiveFrontBarrier|sendFrontBarrier -> commFrontBarrier,
        rLowerFrontBarrier|sLowerFrontBarrier -> cLowerFrontBarrier,
        rLiftFrontBarrier|sLiftFrontBarrier -> cLiftFrontBarrier,

        receiveBackBarrier|sendBackBarrier -> commBackBarrier,
        rLowerBackBarrier|sLowerBackBarrier -> cLowerBackBarrier,
        rLiftBackBarrier|sLiftBackBarrier -> cLiftBackBarrier,

        receiveLock|sendLock -> commLock,
        rEngage|sEngage -> cEngage,
        rDisengage|sDisengage -> cDisengage,

        rLowerDeck|sLowerDeck -> cLowerDeck,
        rLiftDeck|sLiftDeck -> cLiftDeck,
        receiveDeck|sendDeck -> commDeck
      },
         PreSign(off)
      || StopSign(off)
      || FrontBarrier(lift)
      || BackBarrier(lift)
      || Lock(engaged)
      || Deck(down)
      || SafetyLayer
    )
  );
